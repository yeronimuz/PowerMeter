import java.text.SimpleDateFormat

plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id 'net.nemerosa.versioning' version '2.8.2'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'

ext {
    baseName = 'lnb-powermeter'
    classifier = 'FINAL'
}

repositories {
    mavenLocal()
    mavenCentral()
}

mainClassName = 'org.domiot.p1.pmagent.PowerMeterAgent'

// version = versioning.info.full

shadowJar {
    archiveBaseName.set('powermeter')
    archiveClassifier.set('final')
    mergeServiceFiles()
    mainClassName = mainClassName
    // exclude('logback.xml')
}

jar {
    manifest {
        attributes(
                'Implementation-Title': 'Powermeter Agent',
                'Implementation-Version': archiveVersion,
                'Implementation-Classifier': project.ext.classifier,
                'Built-By': System.properties['user.name'],
                'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Build-Revision': versioning.info.commit,
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
    exclude('*.log')
}

jacoco {
    toolVersion = "0.8.11"
    reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'org.domiot'
            artifactId 'p1-meter'
            version version

            from components.java
        }
    }
}

dependencies {
    implementation group: 'org.lankheet.domiot', name: 'domiot-datatypes', version: '0.8.0-SNAPSHOT'
    implementation group: 'org.lankheet.domiot', name: 'domiot-rest-api', version: '1.1.1-SNAPSHOT'

    implementation group: 'org.yaml', name: 'snakeyaml', version: '2.2'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.16.1'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.16.1'
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.16.1'
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.5'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.9'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.5.3'
    implementation("com.github.oshi:oshi-core:6.4.11")


    annotationProcessor 'org.projectlombok:lombok:1.18.28'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.32'

    testImplementation group: 'org.junit.platform', name: 'junit-platform-runner', version: '1.10.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.10.2'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.11.0'
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: '5.2.0'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.28'
}

test {
    ignoreFailures = !project.hasProperty('release')
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}
